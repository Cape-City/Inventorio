plugins {
    id "com.github.johnrengelman.shadow" version "5.0.0"
}

archivesBaseName = "${rootProject.archives_base_name}-1.16-fabric"

configurations {
    shadowCommon
}

architectury {
    platformSetupLoomIde()
    fabric()
}

repositories {
    maven { url "https://maven.terraformersmc.com/" } //Trinkets, ModMenu
    maven { url 'https://jitpack.io' }
}

dependencies {
    //Modding platform
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modImplementation "net.fabricmc:fabric-language-kotlin:${rootProject.fabric_kotlin_version}"

    //Other mods we do integration with
    modCompileOnly "io.github.prospector:modmenu:1.14.13+build.22"
    modCompileOnly("dev.emi:trinkets:${rootProject.trinkets_version}") { transitive = false }
    modCompileOnly("com.github.Geometrically:Gravestones:${rootProject.gravestones_tag}") { transitive = false }
    modCompileOnly("com.github.CerulanLumina:better-graves:${rootProject.bettergraves_tag}") { transitive = false }

    //Other modules of this project
    implementation(project(path: ":common")) {
        transitive = false
    }
    developmentFabric(project(path: ":common")) {
        transitive = false
    }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) {
        transitive = false
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

jar {
    classifier "dev"
}

java {
    withSourcesJar()
    withJavadocJar()
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

javadocJar {
    def commonSources = project(":common").javadoc
    dependsOn commonSources
    from commonSources.destinationDir
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = archivesBaseName
            artifact remapJar
            artifact javadocJar
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }
}